plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk rootProject.compile_sdk_version

    defaultConfig {
        applicationId "com.sliide.app"
        minSdk rootProject.min_sdk_version
        targetSdk rootProject.target_sdk_version
        versionCode getVersionCode()
        versionName getVersionName()

        buildConfigField "String", "SERVER_URL", "\"https://gorest.co.in/public/v1/\""
        buildConfigField "String", "API_ACCESS_TOKEN", getApiAccessToken()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // dependencies are defined in ext arrays in app/build.gradle
    implementation libraries
    kapt librariesKapt

    testImplementation testLibraries
    androidTestImplementation androidTestLibraries
}

static def getVersionCode() {
    // Get CI/CD server environment variable for version code
    def buildNumber = System.getenv('BUILD_NUMBER')

    if (buildNumber != null && buildNumber.isNumber()) {
        return buildNumber.toInteger()
    }
    return 1
}

static def getVersionName() {
    def majorNumber = 0
    def minorNumber = 0
    return majorNumber + "." + minorNumber + "." + getVersionCode()
}

def getApiAccessToken() {
    def fileName = 'api.properties'
    def apiPropertyName = 'apiAccessToken'

    def apiPropertiesFile = rootProject.file(fileName)
    def apiProperties = new Properties()

    if (!apiPropertiesFile.exists()) {
        throw new GradleException("Missing file '$fileName' with property '$apiPropertyName'")
    }
    apiProperties.load(new FileInputStream(apiPropertiesFile))

    return "\"${apiProperties[apiPropertyName]}\""
}